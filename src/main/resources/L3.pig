AIRPORTS = LOAD 'AIRPORTS.csv' USING org.apache.pig.piggybank.storage.CSVExcelStorage() AS (code:int, name:chararray);
AIRPORTS = FILTER AIRPORTS BY code > 0;

FLIGHTS = LOAD 'FLIGHTS.csv' USING org.apache.pig.piggybank.storage.CSVExcelStorage();
FLIGHTS = FOREACH FLIGHTS GENERATE (int) $14 AS code, (float) $18 AS delay;
DELAYED_FLIGHTS = FILTER FLIGHTS BY code > 0 AND delay > 0;

AIRPORTS_FLIGHTS = JOIN AIRPORTS BY code, DELAYED_FLIGHTS by code;
AIRPORTS_FLIGHTS = FOREACH AIRPORTS_FLIGHTS GENERATE AIRPORTS::code AS code, AIRPORTS::name AS name, DELAYED_FLIGHTS::delay AS delay;

GROUPPED_AIRPORTS_FLIGHTS = GROUP AIRPORTS_FLIGHTS BY (code, name);

STATISTICS = FOREACH GROUPPED_AIRPORTS_FLIGHTS GENERATE
    CONCAT(CONCAT('"', group.name), '"'),
    MIN(AIRPORTS_FLIGHTS.delay),
    MAX(AIRPORTS_FLIGHTS.delay),
    AVG(AIRPORTS_FLIGHTS.delay);

dump STATISTICS;